<!DOCTYPE html><html><head><title>Untitled</title></head><body><pre>
;*****************************
;* S/geoCBTXfer
;* (c) 2018 Bo Zimmerman
;*
;* XModem transfer gui and file mgmt.
;* File handlers shared with Buffer options
;* 
;*****************************
.noeqin
.include	GEOSequates
.glbl
.eqin

.psect

; ** jump table for VPRGbase+0, +3, etc
	jmp	DoXUpload
	jmp	DoXDownload




XferInit:	mvw	JComSPut+1,XMOSPut+1
	mvw	JComGet+1,XMOGet+1
	mvw	JComOnline+1,XMOnline+1
	mvw	JPauSec+1,XMOPause+1
	rts

XComReset:	jsr	XMOPause
	jmp	JComReInit




DoXUpload:	jsr	XferInit
	jsr	DoXUBox
	beq	10$
	jmp	CBTTerm
10$:	jsr	ClearScn	; clears the screen
	ldb	flgDoBg,#0
	jmp	XUpload



XUpload:	ldb	XMoreFlg,#$ff	; there is definitely more to get
	ldw	XBufWrPtr,#BACK_SCR_BASE
	ldw	XBufRdPtr,#BACK_SCR_BASE
	ldw	XBytesDone,#0
	ldb	XErrors,#0
	ldw	A2,#XFilename
	ldb	A3L,#$00
	jsr	OpenSF
	cpx	#0
	beq	10$
	ldw	A2,#XUpFlTxt
	jsr	OKBox
	jsr	MnuActDn
	jmp	CBTTerm
10$:	jsr	XComReset
	jsr	UseSystemFont
	jsr	XUpBox
	ldw	A2,#XUpFillBuf
	ldw	A3,#XUpRead
	ldw	A4,#XUpBadBlk
	ldw	A5,#XAbortChk
	jsr	XMOsend
	cpx	#0
	bne	15$
	jsr	XComReset
	jsr	ClearScn	; clears the screen
	ldw	A2,#XUpDnTxt
	jsr	OKBox
	bra	20$
15$:	jsr	XComReset
	jsr	ClearScn	; clears the screen
	ldw	A2,#XUpErTxt
	jsr	OKBox
20$:	jsr	FixDrive
	jmp	CBTTerm

XUpDnTxt:	.byte	27,"Upload Complete!",27,0
XUpErTxt:	.byte	27,"Upload Cancelled.",27,0
XUpFlTxt:	.byte	27,"File failure. Upload cancelled",27,0

XUpFillBuf:	jsr	XUpdBox
	cwi	XBufRdPtr,#BACK_SCR_BASE + $1000
	bcs	20$
	cwi	XBufWrPtr,#BACK_SCR_BASE
	beq	50$	; one time uber-fill
10$:	avw	#128,XBytesDone
	ldx	#0
	rts
20$:	cpw	XBufRdPtr,XBufWrPtr
	beq	10$
	mvw	XBufRdPtr,A6
	ldw	A7,#BACK_SCR_BASE
30$:	ldy	#0
	lda	(A6),y
	sta	(A7),y
	inw	A6
	inw	A7
	cpw	A6,XBufWrPtr
	bcc	30$
	ldw	XBufRdPtr,#BACK_SCR_BASE
	svw	#$1000,XBufWrPtr
	lda	XMoreFlg
	beq	10$
50$:	ldb	XMoreFlg,#0
	mvw	XBufWrPtr,A2
	ldw	A3,#BACK_SCR_BASE + $1800
	jsr	GetSMem
	mvw	A2,XBufWrPtr
	inw	XBufWrPtr
	cpx	#0
	bne	60$
	inc	XMoreFlg
60$:	jsr	XComReset
	ldx	#0
	rts


XUpRead:	cpw	XBufRdPtr,XBufWrPtr
	bne	20$
	lda	XMoreFlg
	bne	10$
	ldx	#$ff
	lda	#$00
	rts
10$:	jsr	XUpFillBuf
	jmp	XUpRead
20$:	ldy	#0
	mvw	XBufRdPtr,A6
	inw	XBufRdPtr
	ldx	#0
	lda	XMoreFlg
	bne	30$
	cpw	XBufRdPtr,XBufWrPtr
	bne	30$
	inx
30$:	lda	(A6),y
	rts	

XUpBadBlk:	inc	XErrors
	jsr	XUpdBox
	rts	

XferBox:	jsr	$c1a8	; i_GraphicsString
	.byte	$01,$5b,$00,$42	; MOVEPENTO Xl,Xh,Y
	.byte	$05,$01	; NEWPATTERN
	.byte	$03,$eb,$00,$79	; RETANGLETO  Xl,Xh,Y
	.byte	$01,$53,$00,$3c	; MOVEPENTO Xl,Xh,Y
	.byte	$05,$00	; NEWPATTERN
	.byte	$03,$e0,$00,$70	; RETANGLETO  Xl,Xh,Y
	.byte	$01,$50,$00,$38	; MOVEPENTO Xl,Xh,Y
	.byte	$07,$e1,$00,$71	; FRAME_RECTO  Xl,Xh,Y

	.byte	$05,$01	; newpattern
	.byte	$01,$d0,$00,$60	; movepento
	.byte	$07,$d8,$00,$68	; framerecto
	.byte	$01,$d0,$00,$60	; movepento
	.byte	$02,$d8,$00,$68	; line
	.byte	$01,$d8,$00,$60	; movepento
	.byte	$02,$d0,$00,$68	; line
	
	.byte	$06,$6b,$00,$65,$18,"Errors:",27,0
	jsr	$c1a8	; i_GraphicsString
	.byte	$06,$6b,$00,$56,$18,"Bytes:",27,0
	rts

XUpBox:	jsr	XferBox
	jsr	$c1a8	; i_GraphicsString
	.byte	$06,$79,$00,$44,$18,"Uploading...",27,0
	jsr	XUpdBox
	rts

XDnBox:	jsr	XferBox
	jsr	$c1a8	; i_GraphicsString
	.byte	$06,$79,$00,$44,$18,"Downloading...",27,0
	jsr	XUpdBox
	rts

XUpdBox:	lda	#0
	jsr	SetPattern
	ldw	R3,#$008e
	ldw	R4,#$00c0
	ldb	R2L,#$50
	ldb	R2H,#$68
	jsr	Rectangle
	ldw	R11,#$0090
	ldb	R1H,#$57
	mvw	XBytesDone,R0
	lda	#$ff
	jsr	PutDecimal
	ldw	R11,#$0090
	ldb	R1H,#$65
	mvb	XErrors,R0L
	ldb	R0H,#0
	lda	#$ff
	jsr	PutDecimal
	rts		; exit from screen routine


XAbortChk:	
	ldb	R2L,#$60
	ldb	R2H,#$68
	ldw	R3,#$00d0
	ldw	R4,#$00d8
	jsr	IsMseInRegion
	cmp	#$00
	beq	90$
	lda	mouseData
	bne	90$
	ldb	R2L,#$60
	ldb	R2H,#$68
	ldw	R3,#$00d0
	ldw	R4,#$00d8
	jsr	InvertRectangle
	sec
	rts
90$:	clc
	rts

DoXDownload: 
	jsr	XferInit
	ldb	XDnFType,#$02
	jsr	DoXDBox
	beq	10$
	jmp	CBTTerm
10$:	jsr	ClearScn	; clears the screen
	ldb	flgDoBg,#0
	jmp	XDownload



XDownload:	ldw	XBufWrPtr,#BACK_SCR_BASE
	ldw	XBufRdPtr,#BACK_SCR_BASE
	ldw	XBytesDone,#0
	ldb	XErrors,#0
	ldw	A2,#XFilename
	mvb	XDnFType,A3L
	jsr	CreatSF
	cpx	#0
	beq	10$
	ldw	A2,#XDnFlTxt
	jsr	OKBox
	jsr	MnuActDn
	jmp	CBTTerm
10$:	jsr	XComReset
	jsr	UseSystemFont
	jsr	XDnBox
	ldw	A2,#XDnRcvByte
	ldw	A3,#XDnGooBlk
	ldw	A4,#XDnBadBlk
	ldw	A5,#XAbortChk
	jsr	XMOrcvCrc
	cpx	#0
	bne	15$
	jsr	XDnFlush
	lda	XDnFType
	ora	#$80
	sta	A2L
	jsr	ClosSF
	jsr	XComReset
	jsr	ClearScn	; clears the screen
	ldw	A2,#XDnDnTxt
	jsr	OKBox
	bra	20$
15$:	ldb	A2L,#$82	; doesnt matter
	jsr	ClosSF
	ldw	R0,#XFilename
	jsr	DeleteFile
	jsr	XComReset
	jsr	ClearScn	; clears the screen
	ldw	A2,#XDnErTxt
	jsr	OKBox
20$:	jsr	FixDrive
	jmp	CBTTerm

XDnDnTxt:	.byte	27,"Download Complete!",27,0
XDnErTxt:	.byte	27,"Download Cancelled.",27,0
XDnFlTxt:	.byte	27,"File failure. Download cancelled",27,0


XDnRcvByte:	pha
	mvw	XBufWrPtr,A6
	inw	XBufWrPtr
	inw	XBytesDone
	ldy	#0
	pla
	sta	(A6),y
	rts	

XDnFlush:	cwi	XBufWrPtr,#BACK_SCR_BASE
	bne	10$
	rts
10$:	ldw	A2,#BACK_SCR_BASE
	mvw	XBufWrPtr,A3
	dew	A3
	jsr	PutSMem
	ldw	XBufWrPtr,#BACK_SCR_BASE
	jsr	XComReset
	rts

XDnGooBlk:	jsr	XUpdBox
	cwi	XBufWrPtr,#BACK_SCR_BASE+$1700
	bcc	10$
	jsr	XDnFlush
10$:	rts


XDnBadBlk:	inc	XErrors
	jsr	XUpdBox
	rts



.ramsect
XBufWrPtr:	.block 2
XBufRdPtr:	.block 2
XMoreFlg:	.block 1
XBytesDone:	.block 2
XErrors:	.block 1

</pre></body></html>