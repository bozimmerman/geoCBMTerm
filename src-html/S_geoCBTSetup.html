<!DOCTYPE html><html><head><title>Untitled</title></head><body><pre>
;*****************************
;* S/geoCBTSetup
;* (c) 2018 Bo Zimmerman
;*
;* Setup menus screens, initialize the 
;* modem, and get everything ready
;* for the first-terminal access.
;*****************************
.if Pass1
.include GEOSequates
.endif

.psect



Setup:	ldw	R9,#$8400	; read the header part
	jsr	GetFHdrInfo
	cpx	#0
	beq	10$
	jmp	EnterDeskTop
10$:	ldb	FlgNoDCD,#0	; first read header args
	ldb	flgEcho,#0	; echo on default
	ldb	flgANSI,#0	; ansi off default
	ldb	BaudCode,#1	; 1200 baud default
	jsr	ReadArgs
	jsr	Check128	; then fix modem driver
	beq	20$
	jsr	Ld64Drvr
20$:	jsr	CreatRW	; now setup REU
	beq	30$
	inc	BuffEnable
30$:	jsr	scnSetup	; clear screen
	ldb	menuInit,#0	; flag un-setup menu
	jsr	mdmSetup	; * Send modem init, and Floff
	; other stuff
	jmp	CBTTerm



;** Read in any arguments
ReadArgs:	ldw	A0,#$8100+160
	ldy	#0
10$:	lda	(A0),y
	bne	13$
	rts
13$:	iny
	cmp	#'-'	; if a switch, go do that
	beq	15$
	cmp	#'='	; if init seq, do that now
	bne	10$	; if neither, next char
	jmp	ReadInitStr
15$:	lda	(A0),y	; get the switch char
	bne	16$
	rts
16$:	jsr	ReadSwitch
	bra	10$

ReadSwitch:	ora	#$20	; always lowercase
	cmp	#'d'
	bne	21$
	inc	FlgNoDCD
21$:	cmp	#'e'
	bne	22$
	inc flgEcho
22$:	cmp	#'a'
	bne	23$
	inc	flgANSI
23$:	cmp	#'b'
	bne	30$
	inc	BaudCode
30$:	rts

ReadInitStr:	ldx	#$ff
12$:	inx		; loop through rest of block
	lda	(A0),y
	beq	13$
	iny
	sta	mdmInitStr+5,x
	bra	12$
13$:	lda	#13	; close the new init str
	sta	mdmInitStr+6,x
	lda	#0
	sta	mdmInitStr+7,x
	rts


Ld64Drvr:	lda	#1	; C64 DRIVER RECORD
	sec
	sbc	#1	; get t&s from table
	asl	a
	tay
	lda	VPRGtabl,y
	sta	R1L	; get t&s from table
	lda	VPRGtabl+1,y
	sta	R1H
	ldw	R7,#$0400
	ldw	R2,#$ffff
	jsr	ReadFile	; read the module in
	rts


scnSetup:	ldb	txtTop,#$10	; screen def
	ldb	txtBot,#199
	ldw	txtLeft,#$00
	ldw	txtRight,#319
	mvw	txtRight,txtREnd	; last chars
	ldb	curWidth+1,#0
	sbw	curWidth,txtREnd
	inw	txtREnd	
	mvb	txtBot,txtBEnd	; bot row
	inc	txtBEnd
	sbb	curHeight,txtBEnd
	mvb	COLOR_MATRIX,origColor
	lda	#0
	jsr	ClrTrmScn
	jsr	UseSystemFont	; * for now
	ldb	dispBufferOn,#ST_WR_FORE
	mvb	baseColor,curColor
	jsr	CrsrTL	
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
	ldb	windowTop,#0	; necc for menu recover?
	mvb	txtBot,windowBottom,
	mvw	txtLeft,leftMargin
	mvw	txtRight,rightMargin
	rts

CrsrTL:	mvb	txtTop,txtXY+2
	ldw	colorLPtr,#COLOR_MATRIX+(40*2)
	mvw	txtLeft,txtXY
	mvw	colorLPtr,colorPPtr
	rts


mdmSetup:	ldb	A2L,#>RS232Buf	; input buffer
	ldb	A2H,#$01;CLBfLen	; input buffer len pages
	ldb	A3L,#$01	; 1200 baud
	ldb	A3H,#$80	; RTSCTS Flow Control!
	ldb	OutBuf,#0
	jsr	JComInit
	jsr	JPauSec
	ldw	R0,#mdmInitStr
	jsr	JComPutS
	jsr	JComFloff
	rts

mdmInitStr:	.byte 13,"  atze1v1x1f0q0r1&p1i0",13,0
	.byte	"                      ",0




</pre></body></html>