<!DOCTYPE html><html><head><title>Untitled</title></head><body><pre>
;*****************************
;* S/geoCBTBufF
;* (c) 2017 Bo Zimmerman
;*
;* Capture Buffer file operations
;*****************************
.if Pass1
.include GEOSequates
.endif

.psect

; ** jmp table VPRGbase +0, +3, etc..
	jmp	doBufSav
	jmp	doBufLod

doBufSav:	jsr	OpenRR
	beq	10$
	ldw	A2,#BufSavNo
	jsr	CNCLBox
	jmp	XferCncl	; we're done.
10$:	ldb	XDnFType,#$02	; first option==ascii
	ldw	FBOXtt0,#FBOXtt2
	ldw	FBOXtt1,#FBOXtt2+5
	jsr	DoXDBox
	beq	20$
	rts		; cancel already done
20$:	ldw	A2,#XFilename
	ldb	A3L,#$01	; seq?
	jsr	CreatSF
	cpx	#0
	beq	30$
	ldw	A2,#BNoCrTxt
	jsr	OKBox
	jmp	XferCncl	; we're done.
30$:	jsr	GetRByte
	cpx	#0
	bne	90$
	jsr	PoorPETSCII
	cmp	#0
	beq	30$
	jsr	PutSByte
	bra	30$
90$:	lda	#$81
	sta	A2L
	jsr	ClosSF
	ldw	A2,#BDnCrTxt
	jsr	OKBox
	jsr	XComReset
	jmp	XferCncl	; we're done.
BufSavNo:	.byte	27,"Buffer Empty?!",0
BNoCrTxt:	.byte	27,"File Create Failure.",0
BDnCrTxt:	.byte	27,"Save Complete.",0


doBufLod:	jsr	DoXUBox
	beq	20$
	rts		; cancel already done
20$:	ldw	A2,#XFilename
	ldb	A3L,#$00
	jsr	OpenSF
	cpx	#0
	beq	30$
	ldw	A2,#BNoOpTxt
	jsr	OKBox
	jsr	XComReset
	jmp	XferCncl	; we're done.
30$:	jsr	CreatRW
35$:	jsr	GetSByte
	cpx	#0
	bne	90$
	jsr	PutRByte
	bra	35$
90$:	lda	#$81
	sta	A2L
	jsr	ClosSF
	ldw	A2,#BDnLdTxt
	jsr	OKBox
	jsr	XComReset
	jmp	XferCncl	; we're done.
BNoOpTxt:	.byte	27,"File Open Failure.",0
BDnLdTxt:	.byte	27,"Load Complete.",0


PoorPETSCII:	ldx	XDnFType
	cpx	#$02
	bne	10$
	rts
10$:	cmp	#$08
	bne	12$
	lda	#$14	; 8->20
11$:	rts
12$:	cmp	#$0d
	beq	11$
	cmp	#$20
	bcs	14$
13$:	lda	#0
	rts
14$:	cmp	#$41
	bcc	11$	; 32-40 is good
	cmp	#$5b
	bcs	20$
	add	#$80
	rts
20$:	cmp	#$61
	bcc	11$
	cmp	#$7b
	bcs	13$
	sec
	sbc	#$20
	rts
	
</pre></body></html>