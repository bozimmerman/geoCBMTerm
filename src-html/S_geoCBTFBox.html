<!DOCTYPE html><html><head><title>Untitled</title></head><body><pre>
;*****************************
;* S/geoCBTFBox
;* (c) 2020 Bo Zimmerman
;*
;* File Box operations
;*****************************
.noeqin
.include	GEOSequates
.glbl
.eqin

.psect


DoXUBox:
05$:	ldb	FBOXnod1,#0
	ldb	FBOXnoo2,#$06	; reset disk icon
	cpb	curDrive,VPRGdriv
	bne	10$
	ldb	FBOXnoo2,#0
10$:	ldw	$885b,#$ff00	; clear file win selections
11$:	jsr	FBOXmvnm
	ldw	R10,#0	; perm filename
	ldb	R7L,#NOT_GEOS	; type of file
	ldw	R0,#FBOXobox
	jsr	DoDlgBox
	lda	sysDBData
	beq	11$
	cmp	#$02	; cancelled?
	bne	20$
15$:	jsr	MnuActDn
	ldx	#$ff
	rts
20$:	cmp	#$06	; change disks?
	bne	23$
	ldw	$885b,#$ff00	; re-clear selection
	jsr	FBOXchgd
	bne	15$
	beq	11$
23$:	cmp	#$05	; something else?
	bne	05$
	ldw	A5,#XFilename	;
	jsr	FBOXopen	; moves filename into XFilename
	lda	XFilename
	beq	15$
	ldx	#0
	rts

PermFName:	.byte	$00

FBOXobox:	.byte	$81
	.byte	16	; getfiles
	.byte	$06,$03
	.byte	19
	.word UpBxGfx
	.byte	11
	.byte	$84,$09
	.word FBOXtxt4
	.byte	12
	.byte	$84,$13
	.byte A6
	.byte	17
	.word FBOXsele
	.byte	$05,$11,$16	; open icon
	.byte	$02,$11,$4c	; cancel icon
FBOXnoo1:	.byte	18	; drive icon
	.byte	$11,$28
	.word FBOXdcon
FBOXnoo2:	.byte $06,$11,$3d
	.byte 0	; end of def
FBOXdcon:	.word FBOXdmag
	.word 0
	.byte	$06, $10
	.word FBOXswdv

UpBxGfx:	;jsr	ReDoMenu	; menu code not avail
	mvw	leftMargin,R3
	mvw	rightMargin,R4
	ldb	R11L,#$0f
	lda	#0
	jsr	HorizontalLine
	rts


FBOXsele:	;This routine handles clicks in the filename box
	lda	mouseData	;was button pressed?
	bmi	94$
	jsr	FBOXckch	;check all filenames for click
	cmp	#$ff
	beq	94$	;no filename was selected
	clc
	adc	$885b
	sta	APITemp2	;save new filename index
	lda	$885c
	cmp	#$ff	;anything clicked before?
	beq	93$	;yes, so exit!
	sec
	sbc	$885b	;find old screen index
	tax
	lda	ClkCht,x	;calculate old screen index
	sta	R2L
	clc
	adc	#$0c
	sta	R2H
	jsr	InvertRectangle	;un-invert old choice
93$:	mvb	APITemp2,$885c	;save new index
94$:	rts


FBOXckch:	;This routine finds which filename is clicked on
	ldb	A6L,#$00	;set temporary index
92$:	ldx	A6L
	lda	ClkCht,x	;calculate filename box dimen.
	sta	R2L	;top
	clc
	adc	#$0c
	sta	R2H	;bottom
	ldw	R3,#$0047	;now set left
	ldw	R4,#$00c1	;  and right
	jsr	IsMseInRegion
	cmp	#$ff	;click was not on that filename
	bne	93$
	jsr	InvertRectangle	;if it was, invert the filename
	lda	A6L	;here is your new index?
	rts		;and exit
93$:	inc	A6L
	lda	A6L
	cbi	A6L,#$05	;otherwise go back and check
	bne	92$	;  the rest.
	lda	#$ff
	rts

ClkCht:	.byte	$24,$32,$40,$4e,$5c,$6a
	;chart for calculating the top and bottom scanlines

FBOXopen:	ldw	A6,#$8300	;Set start of filename grid
	ldx	$885c	;Find filename index
	bne	12$	;If first filename, your done!
	beq	13$
12$:	avw	#$11,A6	;Otherwise go to next filename
	dex
	bne	12$	;Keep on to fname index 
13$:	ldy 	#$00		;Now move filename 
14$:	lda	(A6),y	;   onto valid position
	sta	(A5),y
	iny
	cpy	#$11	;Up to max 17 characters
	bne	14$
	lda	#$00	;Then set EOL character as last
	sta	(A5),y
	rts


DoXDBox:	ldw	A5,#XFilename
	ldy	#0
	tya
04$:	sta	(A5),y	; clear the filename
	iny
	cpy	#18
	bcc	04$
05$:	ldb	FBOXnod2,#$06	; reset disk icon
	cpb	curDrive,VPRGdriv
	bne	10$
	ldb	FBOXnod2,#0	; clear out disk icon
10$:	jsr	FBOXmvnm
	ldw	A5,#XFilename
	ldw	R0,#FBOXcbox	;now perform actual dialog box
	ldw	A6,#FBOXdnam
	jsr	DoDlgBox
	cbi	sysDBData,#$02	;was cancel hit?
	bne	15$
14$:	jsr	MnuActDn	;back to start
	ldx	#$ff
	rts
15$:	cmp	#$06	;change disks
	bne	16$
	jsr	FBOXchgd
	bne	14$	;a change disk error?
	beq	05$
16$:	cmp	#$f0	;if other than filename, repeat
	beq	05$
	ldy	#0
	lda	(A5),y	;non-null filename
	beq	14$
	jsr	FBOXcrve
	cpx	#0
	bne	05$
	rts


FBOXmvnm:
	ldx	#A6	;discover the current drives
	jsr	GetPtrCurDkNm	;name. should this be a routine?
	ldy	#$00
17$:	lda	(A6),y	;copy disk name to buffer...
	cmp	#$a0	;so that a 0 can be added
	beq	18$	;and thus be readable by
	sta	FBOXdnam,y	;the dialog box routines
	iny		
	cmp	#$a0	
	beq	18$
	cpy	#18
	bne	17$
18$:	lda	#0	;now add rol to end
	sta	FBOXdnam,y
	rts


FBOXcbox:	.byte	$80 | $01
	.byte	11	;add text
	.byte	$16,$11
	.word	FBOXtxt4
	.byte	12
	.byte	$43,$11
	.byte	A6
	.byte	11	;add text
	.byte	$16,$22
	.word	FBOXtxt5
	.byte	13	;GetString
	.byte	$1a,$26	;XOffset.Yalso
	.byte	A5	
	.byte	16	;maximum # of chars to accept.
	.byte	15	; do graphics table
	.word	FBOXgfx
	.byte	11	;add text
	.byte	$1e,$40
FBOXtt0:	.word	FBOXttt
	.byte	11	;add text
	.byte	$3d,$40
FBOXtt1:	.word	FBOXttt+4
	.byte	$02,$10,$47	;Cancel icon
	.byte	17
	.word	FBOXclk
FBOXnod1:	.byte	18	;drive icon
	.byte	$09,$47
	.word	FBOXdcn2
FBOXnod2:	.byte	$06,$02,$47	;DISK icon
	.byte	0	;end of defintion

FBOXdcn2:	.word	FBOXdmag	;graphic location
	.word	0	;leave this at 0
	.byte	$06,$10	;width, set below
	.word	FBOXswdv	;location of action routine

FBOXgfx:	.byte	5,0	; pattern 0
	.byte	1,83,0,88	; movepento
	.byte	3,92,0,96	; prg recto
	.byte	1,115,0,88	; movepento
	.byte	3,123,0,96	; seq recto
	.byte	5,1	; pattern 1
XPRGgfx:	.byte	1,83,0,88	; movepento
	.byte	3,92,0,96	; prg recto
XSEQgfx:	.byte	1,115,0,88	; movepento
	.byte	7,123,0,96	; seq recto
FBOXgfnx:	.byte	0
FBOXttt:	.byte	"PRG",0,"SEQ",0
FBOXtt2:	.byte	"ansi",0,"petscii",0




FBOXdmag:
<img src="00064.png" />

FBOXclk:	lda	mouseData
	beq	10$
	rts
10$:	cbi	XDnFType,#$02
	bne	20$
	mvb	XSEQgfx+3,R2L
	mvb	XSEQgfx+7,R2H
	mvw	XSEQgfx+1,R3
	mvw	XSEQgfx+5,R4
	jsr	IsMseInRegion
	cmp	#$00
	bne	15$
	rts
15$:	ldb	XDnFType,#$01
	ldb	XPRGgfx+4,#7
	ldb	XSEQgfx+4,#3
	ldw	R0,#FBOXgfx
	jsr	GraphicsString
	rts
20$:	mvb	XPRGgfx+3,R2L
	mvb	XPRGgfx+7,R2H
	mvw	XPRGgfx+1,R3
	mvw	XPRGgfx+5,R4
	jsr	IsMseInRegion
	cmp	#$00
	bne	25$
	rts
25$:	ldb	XDnFType,#$02
	ldb	XSEQgfx+4,#7
	ldb	XPRGgfx+4,#3
	ldw	R0,#FBOXgfx
	jsr	GraphicsString
	rts


FBOXswdv:	jsr	FBOXswd2
	cpx	#$00	;check for error!
	beq	21$
	jsr	FBOXswd2
	ldb	sysDBData,#$02	;error detected, return abort!
	jmp	RstrFrmDialog
21$:	ldb	sysDBData,#$f0	;denoted Drive icon
	jmp	RstrFrmDialog	;no error, jump back

FBOXswd2:	ldy	curDrive
	iny
	cpy	#11
	bne	11$
	ldy	#8
11$:	tya
	pha
	jsr	SetDevice
	pla
	cmp	curDrive
	beq	12$
	sta	curDrive
	bra	FBOXswd2
12$:	jsr	OpenDisk
	cpx	#0
	bne	FBOXswd2
	rts

FBOXchgd:
10$:	ldw	R0,#FBOXcdbx	;handle swap disk box
	jsr	DoDlgBox
	jsr	OpenDisk	;open new disk
	cpx	#0
	bne	10$
	rts

FBOXcdbx:
	.byte	$80 | $01
	.byte	11	;add text
	.byte	$0e,$12
	.word	FBOXtxt7
	.byte	$01,$0f,$42	;OK icon
	.byte	0	;end of defintion



FBOXcrve:	jsr	GetDirHead	;get BAM into memory
	cpx	#0
	bne	99$	; exit on disk error
	ldw	R5,#$8200	;point to BAM
	jsr	CalcBlksFree	
	lda	R4H
	bne	11$
	lda	R4L
	cmp	#$14	;20 blocks minimum
	bcs	11$
	ldw	A2,#FBOXtxt9	;not enough blocks
10$:	jsr	OKBox	;whine and exit
	ldx	#$ff
99$:	rts
11$:	ldb	$886e,#$ff	;check for file existance
	mvw	A5,R6
	jsr	FindFile
	cpx	#$05	;this or that error?
	beq	13$
	cpx	#$00
	bne	12$
	ldw	A2,#FBOXtxt8
	bra	10$
12$:	ldw	A2,#FBOXtxtA	;other errors
	bra	10$
13$	ldx	#$00
	rts		;enough blocks


FBOXtxt0:	.byte	$18,"Please select an option:",27,0
FBOXtxt1:	.byte	$18,"new work file",27,0
FBOXtxt2:	.byte	$18,"existing file",27,0
FBOXtxt3:	.byte	$18,"to deskTop",27,0
FBOXtxt4:  	.byte	$18,"On Disk:",27,0
FBOXtxt5:	.byte	$18,"Please enter new filename:",27,0
FBOXtxt6:  	.byte	$18,"Enter new file name:",27,0		
FBOXtxt7:  	.byte	$18,"Insert new disk into disk drive.",27,0
FBOXtxt8:  	.byte	$18,"ERROR: File exists!",27,0
FBOXtxt9:  	.byte	$18,"ERROR: Disk full!",27,0
FBOXtxtA:  	.byte	$18,"ERROR: Cannot create!",27,0
XDnFType:	.byte	$02


.ramsect
XFilename:	.block 20
FBOXdnam:	.block 20

</pre></body></html>