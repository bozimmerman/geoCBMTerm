<!DOCTYPE html><html><head><title>Untitled</title></head><body><pre>
;*****************************
;*
;*           geoCBMTerm
;*	(c) 2018 Bo Zimmerman
;*
;*  Started January, 2018
;*  Completed ?, 2018
;*
;* Special thanks to Carlos Santiego
;*****************************

;*****************************
;* S/geoCBMTerm
;* (c) 2018 Bo Zimmerman
;*
;* Includes:
;*  Initialization of screen, modem
;*  Low level Char drawing
;*  Low level screen manipulation
;*  Key and Modem input handling
;*****************************

.if Pass1
.noeqin
.noglbl
.include GEOSequates
.eqin
.glbl
.endif

.psect


StartTerm:	lda	menuInit
	bne	10$
	jsr	scnReset
	ldw	A2,#0	; "About" refresh
	ldw	A3,#DAdone	; DA refresh
	ldw	A4,#DAinit	; DA init
	ldw	A5,#AboutStrs
	ldw	fontPtr,#GEOSFonB
	jsr	InitDaAb
	jsr	DrawMenu	; draw the menu if not already
10$:	jsr	FixCurFont
	jsr	FixEcho	; fix menu options 
	jsr	FixATerm
	jsr	Check128
	bne	20$
	lda	#$38	; flag for C128
	sta	CT64128A
	sta	CT64128B
20$:	jsr	keySetup
	jsr	JComEnable	; restore disabled modem driver
	mvw	colorPPtr,colorPtr
	rts

Check128:	lda	#$12
	cmp	$c00f
	bpl	10$
	lda	$c013
10$:	cmp	#$80
	rts

CleanExit:	jsr	PromptOff
	jsr	keyRestore
	jsr	mdmDone
	jsr	SavColorScn	; restores origColor
	jmp	EnterDeskTop


StopTerm:	mvw	colorPtr,colorPPtr
	jsr	SavColorScn
	jsr	ScreenSafe
	jsr	keyRestore
	rts

FixCurFont:	mvw	fontPtr,R0
	jsr	LoadCharSet
	ldb	baselineOffset,#0
	ldb	curWidth,#$08	; curWidth=realSize
	rts




mdmDoneStr:	.byte 13,"  atzb1200",13,0

mdmDone:	jsr	JComDisable
	jsr	JComUninst
	rts



keySetup:	mvw	keyVector,savKeyVec
	sei
	ldw	keyVector,#KeyPRtn
	ldw	intBotVector,#MainLp
	cli
	ldy	curHeight
	dey
	tya	
	jsr	InitTextPrompt
	mvw	txtXY,stringX
	mvb	txtXY+2,stringY
	jsr	PromptOn
	rts



KeyPRtn:	lda	keyData
	bne	10$
	rts
10$:	; *** TODO:BZ: convert ascii to petscii for buff
	ldx	BufferFlg
	beq	12$
	pha
	jsr	PutRByte
	pla
12$:	ldx	flgEcho
	beq	50$
	cmp	#$08
	beq	50$
	cmp	#$1d
	bne	20$
	lda	#$08
20$:	jsr	SafeDraw
50$:	lda	keyData
	jsr	KeyComPut
	rts

KeyComPut:	ldx	flgANSI
	beq	30$
	ldx	#0
11$:	cmp	KeyACt,x
	beq	20$
	inx
	inx
	cpx	#$08
	bcc	11$
	bcs	30$
20$:	inx
	lda	KeyACt,x
	pha
	lda	#27
	jsr	JComSPut
	lda	#91
	jsr	JComSPut
	pla
30$:	cmp	#$1d
	bne	31$
	lda	#$08
31$:	cmp	#$16
	bne	40$
39$:	lda	#$1b
40$:	cmp	#$14
	beq	39$
	pha
	jsr	JComSPut
	pla
	rts
KeyACt:	.byte 16,65,17,66,8,68,30,67,0,0

DrawStr:	mvb	txtXY+2,R1H
	mvw	txtXY,R11
DrwStr2:	mvw	R0,DrwStrLp+3
DrwStrLp:	ldy	#0
	lda	$9999
	beq	30$
20$:	jsr	DrawChar
	inw	DrwStrLp+3
	bra	DrwStrLp	
30$:	mvw R11,txtXY
	mvb	R1H,txtXY+2
	rts

SafeDraw:	pha
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
	pla
	jsr	DrawChar
	mvw R11,txtXY
	mvb	R1H,txtXY+2
	rts



DrawChar:	; ***** TODO:BZ:Convert PETSCII to **FONT** code
	cmp	#$0d
	bne	15$
	jmp	DrwCRLF
15$:	cmp	#$08	; ****** TODO:BZ:NOT RIGHT
	bne	20$
	jmp	PutDel
20$:	cmp	#$20
	bcs	30$
29$:	rts
30$:	cmp	#$7f
	bcs	29$
	jsr	PutChar	; small put char?
	lda	curColor
	.byte	$8d	; sta
colorPtr:	.word COLOR_MATRIX
	lda	R11L
	and	#$04	; even works on 8-pt fonts!
	bne	10$
	inw	colorPtr
10$:	inc	flgDoBg
	cpw	R11,txtRight
	bcs	DrwCRLF
	rts


DrwCRLF:	jsr	DrawCR
	jmp	DrawLF

DrawCR:	mvw	txtLeft,R11
	mvw	colorLPtr,colorPtr
	rts

DrawLF:	cpb	R1H,txtBEnd
	bcs	10$
	adb	curHeight,R1H
	cwi	colorLPtr,#$8fc0
	bcs	05$
	avw	#40,colorLPtr
	avw	#40,colorPtr
05$:	rts
10$:	ldw	R0,#SCREEN_BASE+(24*40)
	ldw	R1,#SCREEN_BASE+(16*40)
	ldw	R2,#$1f3f-(24*40)
	jsr	MyMoveData
	ldw	R0,#COLOR_MATRIX+(3*40)
	ldw	R1,#COLOR_MATRIX+(2*40)
	ldw	R2,#$03e8-(3*40)
	jsr	MyMoveData
	ldw	R1,#SCREEN_BASE+$1f3f-(8*40)
	ldw	R0,#8*40
	ldb	R2L,#0
	jsr	FillRam
	ldw	R1,#BACK_SCR_BASE+$1f3f-(8*40)
	ldw	R0,#8*40
	ldb	R2L,#0
	jsr	FillRam
	ldb	flgDoBg,#1
	ldw	colorLPtr,#$8fc0
	mvb	txtBot,R1H
	sbb	curHeight,R1H
	inc	R1H
	jsr	ClrColLn
	rts

ClrColLn:	tya
	pha
	mvw	colorLPtr,R0
	ldy	#0
	lda	baseColor
10$:	sta	(R0),y
	iny
	cpy	#40
	bcc	10$
20$:	pla
	tay
	rts



PutDel:	cwi	R11,#3	; todo: txtLeft
	bcs	15$
	mvw	txtRight,R11
	cbi	R1H,#$20	; todo: txtHeight?
	bcc	15$
	sbb	curHeight,R1H
	svw	#40,colorLPtr
	mvw	colorLPtr,colorPtr
	avw	#40,colorPtr
15$:	ldb	curWidth+1,#0
	sbw	curWidth,R11	; realSize, #$04
	lda	#$20
	jsr	SmallPutChar
	ldb	curWidth+1,#0
	sbw	curWidth,R11
	lda	R11L
	and	#$04
	beq	20$
	dew	colorPtr
20$:	ldb	flgDoBg,#1
	rts


CrsrTL:	mvb	txtTop,txtXY+2
	ldw	colorLPtr,#COLOR_MATRIX+(40*2)
	jsr	CrsrStLn
	rts

CrsrStLn:	mvw	txtLeft,txtXY
	mvw	colorLPtr,colorPtr
	rts

scnReset:	mvb	baseColor,curColor
	jsr	CrsrTL	
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
	ldb	windowTop,#0	; necc for menu recover?
	mvb	txtBot,windowBottom,
	mvw	txtLeft,leftMargin
	mvw	txtRight,rightMargin
	rts


CrsrUp:	cpb	txtTop,txtXY+2	; Cursor Up
	bcc	10$
	rts
10$:	svw	#40,colorLPtr
	svw	#40,colorPtr
	sbb	curHeight,txtXY+2
	rts

CrsrDn:	cpb	txtXY+2,txtBEnd	; Cursor Down
	bcc	10$
09$:	rts
10$:	adb	curHeight,txtXY+2
	cwi	colorLPtr,#$8fc0
	bcs	09$
	avw	#40,colorLPtr
	avw	#40,colorPtr
	rts

CrsrRt:	cpw	txtXY,txtREnd	; Cursor Forward
	bcc	10$
	rts
10$:	ldb	curWidth+1,#0
	adw	curWidth,txtXY
	lda	txtXY
	and	#$04
	bne	20$
	inw	colorPtr
20$:	rts

CrsrLt:	cpw	txtLeft,txtXY	; Cursor Backward
	bcc	10$
	rts
10$:	ldb	curWidth+1,#0
	sbw	curWidth,txtXY
	lda	txtXY
	and	#$04
	beq	20$
	dew	colorPtr
20$:	rts



QOutC:	sty	QOutC2+1
	inc	OutBuf
	ldy	OutBuf
	sta	OutBuf,y
	jsr	JComFloff
QOutC2:	ldy	#0
	rts

QOutStr:	sty	QOutStr2+1
	ldy	#0	
QOutStr1:	lda	(R0),y
	beq	QOutStr2
	jsr	QOutC
	iny
	bne	QOutStr1
QOutStr2:	ldy	#0	
	rts

QFlush:	lda	OutBuf
	bne	10$
	rts
10$:	ldy	#1
20$:	lda	OutBuf,y
	jsr	JComSPut
	iny
	cpy	OutBuf
	bcc	20$
	beq	20$
	ldb	OutBuf,#0
	jsr	JComFlon
	rts
	


MainLp:	mvb	mdmRdRate,A5L
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
MainLp2:	jsr	JComGet
	bcc	20$
	lda	flgDoBg
	beq	10$
	mvw	R11,stringX
	mvb	R1H,stringY
	jsr	PromptOn
10$:	lda	A9L
	bne	11$
	inc	A9L
	lda	$dc08
	sta	A9H
11$:	cpb	$dc08,A9H
	beq	19$
	mvb	$dc08,A9H
	inc	A9L
	cbi	A9L,#$02
	bcc	19$
	ldb	A9L,#0
	jsr	QFlush
19$:	mvb	R1H,txtXY+2
	mvw	R11,txtXY
	rts
20$:	tax
	ldb	A9L,#0
CT64128A:	.byte	$18	; clc=64, sec=$38
	bcc	10$
	phb	BANK_REG	; c128 save
	ldb	BANK_REG,#BANK_vSTD
	bne	20$
10$:	phb	$01	; c64 save
	ldb	$01, #$30
20$:	txa
	.byte	$a2	; ldx immediate
BufferFlg:	.byte 0
	beq	25$
	pha
	jsr	PutRByte
	pla
25$:	jsr	DrawChar
CT64128B:	.byte	$18		; clc=64, sec=$38
	bcc	10$
	plb	BANK_REG
	bra	20$
10$:	plb	$01
20$:	dec	A5L
	beq	30$
	jmp	MainLp2
30$:	mvb	R1H,txtXY+2
	mvw	R11,txtXY
	rts


; that's all folks!

</pre></body></html>