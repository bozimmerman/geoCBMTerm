<!DOCTYPE html><html><head><title>Untitled</title></head><body><pre>
;*****************************
;*
;*           geoCBMTerm
;*	(c) 2018 Bo Zimmerman
;*
;*  Started January, 2018
;*  Completed ?, 2018
;*
;* Special thanks to Carlos Santiego
;*****************************

;*****************************
;* S/geoCBMTerm
;* (c) 2018 Bo Zimmerman
;*
;* Includes:
;*  Initialization of screen, modem
;*  Low level Char drawing
;*  Low level screen manipulation
;*  Key and Modem input handling
;*****************************

.if Pass1
.noeqin
.noglbl
.include GEOSequates
.eqin			; if you run out of symbols...
.glbl			; you can use these selectively
.endif

.psect


StartTerm:	lda	menuInit
	bne	10$
	jsr	scnReset
	ldw	A2,#0	; "About" refresh
	ldw	A3,#DAdone	; DA refresh
	ldw	A4,#DAinit	; DA init
	ldw	A5,#AboutStrs	
	jsr	InitDaAb
	jsr	DrawMenu	; draw the menu if not already
	jsr	FixBaud	; marks curr baud rate on menu
	ldw	fontPtr,#GEOSFonB
	ldb	flgREV,#0
	ldb	flgUP,#0
10$:	ldb	fontSflg+1,#$00	; initialize the font pointer
	cwi	fontPtr,#GEOSFonB
	beq	13$
	ldb	fontSflg+1,#$80
13$:	ldw	p2fonTab+1,#LP2FONT
	lda	flgUP
	beq	15$
	ldw	p2fonTab+1,#UP2FONT
15$:	jsr	FixCurFont
	jsr	FixEcho	; fix menu options 
	jsr	FixATerm
	jsr	Check128
	bne	20$
	lda	#$38	; flag for C128
	sta	CT64128A
	sta	CT64128B
20$:	jsr	keySetup
	jsr	JComEnable	; restore disabled modem driver
	jsr	JComFlon
	mvw	colorPPtr,colorPtr
	rts

Check128:	lda	#$12
	cmp	$c00f
	bpl	10$
	lda	$c013
10$:	cmp	#$80
	rts



CleanExit:	jsr	PromptOff
	jsr	keyRestore
	jsr	mdmDone
	jsr	SavColorScn	; restores origColor
	jmp	EnterDeskTop


StopTerm:	mvw	colorPtr,colorPPtr
	jsr	SavColorScn
	jsr	ScreenSafe
	jsr	keyRestore
	rts

FixCurFont:	mvw	fontPtr,R0
	jsr	LoadCharSet
	ldb	baselineOffset,#0
	ldb	curWidth,#$08	; curWidth=realSize
	rts

FixNewFont:	mvw	fontPtr,R0
	jsr	LoadCharSet
	rts


mdmDoneStr:	.byte 13,"  atzb1200",13,0

mdmDone:	jsr	JComDisable
	jsr	JComUninst
	rts



keySetup:	mvw	keyVector,savKeyVec
	sei
	ldw	keyVector,#KeyPRtn
	ldw	intBotVector,#MainLp
	cli
	ldy	curHeight
	dey
	tya	
	jsr	InitTextPrompt
	mvw	txtXY,stringX
	mvb	txtXY+2,stringY
	jsr	PromptOn
	rts



KeyPRtn:	lda	keyData
	bne	10$
99$:	rts
10$:	tax		; start key press process
	lda	flgCNTRL	; check for control key
	bne	11$
	lda	GOA2PET,x	; no cntrl key, so nrml lookup
	beq	99$
	bne	20$
11$:	txa		; cntrl key, so filter, then..
	and	#$7f
	tax
	lda	CTL2PET,x	; do special table lookup
	beq	99$
20$:	ldx	BufferFlg	; now we have a PETSCII!
	beq	30$	; check if Buffering
	pha
	jsr	PutRByte	; if buffering, send ch to buff
	pla
30$:	ldx	flgEcho	; Test For Echo
	beq	50$
40$:	pha
	jsr	SafeDraw
	pla
50$:	pha
	jsr	JComSPut	; finally, send char to modem
	pla
	rts


DrawStr:	mvb	txtXY+2,R1H
	mvw	txtXY,R11
DrwStr2:	mvw	R0,DrwStrLp+3
DrwStrLp:	ldy	#0
	lda	$9999
	beq	30$
20$:	jsr	DrawChar
	inw	DrwStrLp+3
	bra	DrwStrLp	
30$:	mvw R11,txtXY
	mvb	R1H,txtXY+2
	rts

SafeDraw:	pha
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
	pla
	jsr	DrawChar
	mvw R11,txtXY
	mvb	R1H,txtXY+2
	rts



DrawChar:	tax
p2fonTab:	lda	LP2FONT,x	; convert PETSCII to font code
	bne	DrawChFn	; if its non-0, jump down to draw
	txa		; if NOT, grab special routine
	bmi	10$	; if HIGH special, use LOW table
	and	#$1f
	asl	a
	tax
	lda	CNTRLCH1,x	; low special routine (<32)
	sta	DrawSpec+1
	inx
	lda	CNTRLCH1,x
	beq	20$
	sta	DrawSpec+2
	bne	DrawSpec	; done setting spec 1, GO!
10$:	and	#$1f	; * start of HIGH special tab
	asl	a
	tax
	lda	CNTRLCH2,x	; high special routine (128-128+32)
	sta	DrawSpec+1
	inx
	lda	CNTRLCH2,x
	beq	20$
	sta	DrawSpec+2
	bne	DrawSpec	
20$:	rts
DrawSpec:	jmp	PutDel	; Do special processing routine
DrawChFn:	pha
	and	#$80		; ** beginning of normal ch draw
fontSflg:	cmp	#$00	; check bit most high
	beq	90$	; if current font is good, draw	
	pla		; otherwise, time to switch
	bmi	10$	; if high bit, then fonA
	pha
	ldb	fontSflg+1,#$00
	ldw	fontPtr,#GEOSFonB	; set font B
	jsr	FixCurFont
	bra	80$
10$:	pha
	ldb	fontSflg+1,#$80
	ldw	fontPtr,#GEOSFonA	; set font A
	jsr	FixCurFont
80$:	; do nothing dunno if R1H,R11 are destroyed or not
90$:	pla		; finally, draw the char
	and	#$7f
	jsr	PutChar	; small put char?
	lda	curColor
	.byte	$8d	; sta
colorPtr:	.word COLOR_MATRIX
	inw	colorPtr
10$:	inc	flgDoBg
	cpw	R11,txtRight
	bcs	DrwCRLF
	rts


DrwCRLF:	jsr	DrawCR
	jmp	DrawLF

DrawCR:	mvw	txtLeft,R11
	mvw	colorLPtr,colorPtr
	rts

DrawLF:	cpb	R1H,txtBEnd
	bcs	10$
	adb	curHeight,R1H
	cwi	colorLPtr,#$8fc0
	bcs	05$
	avw	#40,colorLPtr	; needs 80 col fix
	avw	#40,colorPtr
05$:	rts
10$:	ldw	R0,#SCREEN_BASE+(24*40)
	ldw	R1,#SCREEN_BASE+(16*40)
	ldw	R2,#$1f3f-(24*40)
	jsr	MyMoveData
	ldw	R0,#COLOR_MATRIX+(3*40)
	ldw	R1,#COLOR_MATRIX+(2*40)
	ldw	R2,#$03e8-(3*40)
	jsr	MyMoveData
	ldw	R1,#SCREEN_BASE+$1f3f-(8*40)
	ldw	R0,#8*40
	ldb	R2L,#0
	jsr	FillRam
	ldw	R1,#BACK_SCR_BASE+$1f3f-(8*40)
	ldw	R0,#8*40
	ldb	R2L,#0
	jsr	FillRam
	ldb	flgDoBg,#1
	ldw	colorLPtr,#$8fc0
	mvb	txtBot,R1H
	sbb	curHeight,R1H
	inc	R1H
	jsr	ClrColLn
	rts

ClrColLn:	tya
	pha
	mvw	colorLPtr,R0
	ldy	#0
	lda	baseColor
10$:	sta	(R0),y
	iny
	cpy	#40
	bcc	10$
20$:	pla
	tay
	rts



PutDel:	cwi	R11,#3	; todo: txtLeft
	bcc	10$
	jmp	PutDel1
10$:	cbi	R1H,#$20	; todo: txtHeight?
	bcs	20$
	rts
20$:	mvw	txtRight,R11
	sbb	curHeight,R1H
	svw	#40,colorLPtr
	ldb	curWidth+1,#0
	sbw	curWidth,R11	; realSize
	lda	#$20
	jsr	SmallPutChar
	jmp	PutDel2
PutDel1:	mvb	R1H,txtXY+2
	mvw	R11,txtXY
	ldx	R1H
	jsr	GetScanLine
	mvw	R5,R0
	adw	R11,R0
	mvw	R0,R1
	svw	#8,R1
	ldw	R2,#320	; change this for 80 col
	sbw	txtXY,R2
	jsr	MyMoveData
	mvw	colorPtr,R0
	mvw	colorPtr,R1
	sbw	colorLPtr,R1
	ldw	R2,#40
	sbw	R1,R2
	mvw	R0,R1
	dew	R1
	jsr	MyMoveData
	mvb	txtXY+2,R1H
	ldw	R11,#312	; also, change this for 80 col
	lda	#$20	; will ignore reverse, and font!
	jsr	SmallPutChar
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
PutDel2:	ldb	curWidth+1,#0
	sbw	curWidth,R11
	dew	colorPtr
20$:	ldb	flgDoBg,#1
	rts



DrawIns:	cpw	txtXY, txtRight
	bcc	20$
10$:	rts
20$:	mvb	R1H,txtXY+2
	mvw	R11,txtXY
	ldx	R1H
	jsr	GetScanLine
	ldb	curWidth+1,#0
	mvw	R5,R0
	adw	R11,R0
	mvw	R0,R1
	adw	curWidth,R1
	ldw	R2,#320	; change this for 80 col
	sbw	txtXY,R2
	sbw	curWidth,R2	; because we are copying all but
	jsr	MyMoveData
DrawIns2:	mvw	colorPtr,R0	; i hate the label here
	mvw	colorPtr,R1
	sbw	colorLPtr,R1
	ldw	R2,#40	; 80 col probl
	sbw	R1,R2
	dew	R2
	mvw	R0,R1
	inw	R1
	jsr	MyMoveData
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
	lda	#$20	; will ignore reverse, and font!
	jsr	SmallPutChar
	ldb	curWidth+1,#0
	adw	curWidth,R11
	ldb	flgDoBg,#1
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
	rts



CrsrTL:	mvb	txtTop,R1H
	ldw	colorLPtr,#COLOR_MATRIX+(40*2)
	jsr	CrsrStLn
	rts

CrsrStLn:	mvw	txtLeft,R11
	mvw	colorLPtr,colorPtr
	rts

scnReset:	mvb	baseColor,curColor
	jsr	CrsrTL	
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
	ldb	windowTop,#0	; necc for menu recover?
	mvb	txtBot,windowBottom,
	mvw	txtLeft,leftMargin
	mvw	txtRight,rightMargin
	rts


CrsrUp:	cpb	txtTop,R1H	; Cursor Up
	bcc	10$
	rts
10$:	svw	#40,colorLPtr		; 80 col unfixed
	svw	#40,colorPtr
	sbb	curHeight,R1H
	rts

CrsrDn:	jmp	DrawLF

CrsrRt:	cpw	R11,txtREnd	; Cursor Forward
	bcc	20$
	cpb	R1H,txtBEnd
	bcc	10$
	rts
10$:	ldw	R11,#0
	mvw	colorLPtr,colorPtr
	jmp	CrsrDn
20$:	ldb	curWidth+1,#0
	adw	curWidth,R11
	inw	colorPtr
	cpw	R11,txtREnd	; Cursor Forward
	bcc	30$
	cpb	R1H,txtBEnd
	bcc	10$
30$:	rts

CrsrLt:	cpw	txtLeft,R11	; Cursor Backward	
	bcc	20$
	cbi	R1H,#$20	; todo: txtHeight?
	bcs	10$
	rts
10$:	mvw	txtRight,R11
	svw	#8,R11
	mvw	colorLPtr,colorPtr
	avw	#39,colorPtr	; fix for 80 col
	jmp	CrsrUp
20$:	ldb	curWidth+1,#0
	sbw	curWidth,R11
	dew	colorPtr
30$:	rts



QOutC:	sty	QOutC2+1
	inc	OutBuf
	ldy	OutBuf
	sta	OutBuf,y
	jsr	JComFloff
QOutC2:	ldy	#0
	rts

QOutStr:	sty	QOutStr2+1
	ldy	#0	
QOutStr1:	lda	(R0),y
	beq	QOutStr2
	jsr	QOutC
	iny
	bne	QOutStr1
QOutStr2:	ldy	#0	
	rts

QFlush:	lda	OutBuf
	bne	10$
	rts
10$:	ldy	#1
20$:	lda	OutBuf,y
	jsr	JComSPut
	iny
	cpy	OutBuf
	bcc	20$
	beq	20$
	ldb	OutBuf,#0
	jsr	JComFlon
	rts
	


MainLp:	jsr	KeyChex	; check CNTRL/C=
	jsr	JComGet	; check for a modem char
	bcc	GotMByte
	lda	flgDoBg	; check for crsr mv		
	beq	10$
	mvw	txtXY,stringX	; screen changed, update cursor
	mvb	txtXY+2,stringY
	jsr	PromptOn
10$:	rts
GotMByte:	tax		; got First char! setup
	mvb	mdmRdRate,A5L
	mvb	txtXY+2,R1H	; setup draw char pos
	mvw	txtXY,R11
	jmp	MainDrCh	; go process char in x	
NextBLp:	jsr	JComGet
	bcc	10$
	mvb	R1H,txtXY+2
	mvw	R11,txtXY
	jmp	BuffFlush	
10$:	tax
MainDrCh:	ldb	A9L,#0	; main draw char = .x
CT64128A:	.byte	$18	; clc=64, sec=$38, C128 chk
	bcc	10$
	phb	BANK_REG	; c128 save
	ldb	BANK_REG,#BANK_vSTD
	bne	20$
10$:	phb	$01	; c64 save
	ldb	$01, #$30
20$:	txa		; main draw char = .a
	.byte	$a2	; ldx immediate
BufferFlg:	.byte 0	; buffering ON flag HERE
	beq	25$
	pha
	jsr	PutRByte	; write byte to buffer
	pla
25$:	jsr	DrawChar	; draw byte in .a
CT64128B:	.byte	$18		; clc=64, sec=$38
	bcc	10$
	plb	BANK_REG	; restore from I/O config
	bra	20$
10$:	plb	$01
20$:	dec	A5L	; modem read rate?
	beq	30$	; leave if we did too much
	jmp	NextBLp	; otherwise, get next char
30$:	mvb	R1H,txtXY+2	; we are done, save draw pos
	mvw	R11,txtXY
	rts



KeyChex:	ldb	$dc00,#$7f	; read cia for cntrl key
	lda	$dc01
	tax
	and	#$04
	eor	#$04
	sta	flgCNTRL
	txa		; now look for c= key
	and	#$20
	beq	20$	; if 0, then held down
10$:	rts
20$:	ldb	$dc00,#$bf
	lda	$dc01	; check for right shift
	and	#$10
	beq	30$	; got right shift!
	ldb	$dc00,#$fd
	lda	$dc01
	and	#$80	; check left shift
	bne	10$
30$:	ldw	p2fonTab+1,#LP2FONT
	lda	flgUP
	bne	40$
	ldw	p2fonTab+1,#UP2FONT
	ldb	flgUp,#$ff
	rts
40$:	ldb	flgUp,#0
	rts



BuffFlush:	lda	A9L
	bne	11$
	inc	A9L
	lda	$dc08
	sta	A9H
11$:	cpb	$dc08,A9H
	beq	19$
	mvb	$dc08,A9H
	inc	A9L
	cbi	A9L,#$02
	bcc	19$
	ldb	A9L,#0
	jsr	QFlush
19$:	rts

.ramsect
flgCNTRL:	.block	1
; that's all folks!

</pre></body></html>